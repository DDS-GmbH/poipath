plugins {
    id 'java'
    id 'maven-publish'
}

group = 'com.docutools'
version = '1.0.3'
sourceCompatibility = 14
targetCompatibility = 14

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'poipath'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
        }
    }
    repositories {
        maven {
            url "s3://docu-tools-maven/${project.version.endsWith('-SNAPSHOT') ? 'snapshot' : 'release'}"
            credentials(AwsCredentials) {
                accessKey project.hasProperty('AWS_ACCESS_KEY') ? AWS_ACCESS_KEY : "${System.env.AWS_ACCESS_KEY}"
                secretKey project.hasProperty('AWS_SECRET_KEY') ? AWS_SECRET_KEY : "${System.env.AWS_SECRET_KEY}"
            }
        }
//        maven {
//            name = "GitHubPackages"
//            url "https://maven.pkg.github.com/codecitizen/poipath"
//            credentials {
////                username = project.findProperty("gpr.user") ?: System.getenv("GH_USERNAME")
////                password = project.findProperty("gpr.key") ?: System.getenv("WRITE_TOKEN")
//                username project.hasProperty('GH_USERNAME') ? GH_USERNAME : "${System.env.GH_USERNAME}"
//                password project.hasProperty('WRITE_TOKEN') ? WRITE_TOKEN : "${System.env.WRITE_TOKEN}"
//            }
//        }
    }
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.12'
    annotationProcessor 'org.projectlombok:lombok:1.18.12'
    testImplementation('org.junit.jupiter:junit-jupiter:5.6.0')
    testImplementation("org.hamcrest:hamcrest:2.2")

    implementation("org.apache.poi:poi:4.1.2")
    implementation("org.apache.poi:poi-ooxml:4.1.2")
    implementation("org.apache.poi:poi-ooxml-schemas:4.1.2")
}

// Enable support for records, patterns and text blocks
compileJava {
    options.compilerArgs += ["--enable-preview"]
}
compileTestJava {
    options.compilerArgs += ["--enable-preview"]
}
test {
    jvmArgs '--enable-preview'
    useJUnitPlatform()
}