plugins {
    id 'java'
    id 'maven-publish'
    id "org.owasp.dependencycheck" version "5.2.4"
    id 'checkstyle'
}

group = 'com.docutools'
version = '1.0.5'
sourceCompatibility = 15
targetCompatibility = 15

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'poipath'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url "https://maven.pkg.github.com/AntonOellerer/poipath"
            credentials {
                username = project.hasProperty('GH_USERNAME') ? GH_USERNAME : "${System.env.GH_USERNAME}"
                password = project.hasProperty('WRITE_TOKEN') ? WRITE_TOKEN : "${System.env.WRITE_TOKEN}"
            }
        }
    }
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.12'
    annotationProcessor 'org.projectlombok:lombok:1.18.12'
    testImplementation('org.junit.jupiter:junit-jupiter:5.6.0')
    testImplementation("org.hamcrest:hamcrest:2.2")

    implementation("org.apache.poi:poi:4.1.2")
    implementation("org.apache.poi:poi-ooxml:4.1.2")
    implementation("org.apache.poi:poi-ooxml-schemas:4.1.2")
}

test {
    useJUnitPlatform()
}

dependencyCheck {
    format = 'ALL'
    junitFailOnCVSS = 7.0
    failBuildOnCVSS = 9.0
    suppressionFile = 'config/dependency-check/suppression.xml'
}
tasks.withType(Checkstyle) {
    ignoreFailures = false
    maxWarnings = 0 // Maximum number of warnings allowed
    reports {
        html.destination rootProject.file("build/reports/checkstyle.html")
    }
}