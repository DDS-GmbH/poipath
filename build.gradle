plugins {
    id 'java'
    id 'maven-publish'
    id "org.owasp.dependencycheck" version "5.2.4"
    id 'checkstyle'
}

group = 'com.docutools'
version = '1.1.2'
sourceCompatibility = 15
targetCompatibility = 15

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'poipath'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url "https://maven.pkg.github.com/DDS-GmbH/poipath"
            credentials {
                username = "codecitizen"
                password = project.findProperty("GITHUB_TOKEN") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.12'
    annotationProcessor 'org.projectlombok:lombok:1.18.12'
    testImplementation('org.junit.jupiter:junit-jupiter:5.6.0')
    testImplementation("org.hamcrest:hamcrest:2.2")

    implementation("org.apache.poi:poi:4.1.2")
    implementation("org.apache.poi:poi-ooxml:4.1.2")
    implementation("org.apache.poi:poi-ooxml-schemas:4.1.2")
}

tasks.withType(JavaCompile) {
    options.compilerArgs += "--enable-preview"
}

java {
    withSourcesJar()
}

test {
    jvmArgs '--enable-preview'
    useJUnitPlatform()
}

dependencyCheck {
    format = 'ALL'
    junitFailOnCVSS = 7.0
    failBuildOnCVSS = 9.0
    suppressionFile = 'config/dependency-check/suppression.xml'
}
tasks.withType(Checkstyle) {
    ignoreFailures = false
    maxWarnings = 0 // Maximum number of warnings allowed
    reports {
        html.destination rootProject.file("build/reports/checkstyle.html")
    }
}